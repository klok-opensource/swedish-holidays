plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

group = 'dev.klok.holidays'
version = '1.1.0'

def junitTestVersion = '5.8.2'

java {
    withSourcesJar()
    withJavadocJar()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.debug = false // reduce classfile size
}

tasks.withType(Jar).configureEach {
    // Reproducible jars: stable ordering and timestamps
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    // Minimal manifest
    manifest.attributes(
            'Implementation-Title': 'swedish-holidays',
            'Implementation-Version': project.version
    )
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + junitTestVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-params:' + junitTestVersion
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + junitTestVersion

    /// JMH dependencies (avoid on main classpath)
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

test {
    useJUnitPlatform()
    jvmArgs = [
            '-XX:+UseStringDeduplication'
    ]
}

// JReleaser configuration for signing & deployment
jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    maxRetries = 90
                    retryDelay = 20
                }
            }
        }
    }

    dryrun = false
}

// Publishing configuration for Maven Central
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'swedish-holidays'
            from components.java
            pom {
                name = "Klok Swedish Holidays Utility Library"
                description = "A lightweight Java library providing utilities to determine Swedish standard non-working days."
                url = "https://github.com/klok-opensource/swedish-holidays"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "jakobthun"
                        name = "Jakob Thun"
                        email = "jakob.thun@integrera.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/klok-opensource/swedish-holidays.git"
                    developerConnection = "scm:git:ssh://github.com/klok-opensource/swedish-holidays.git"
                    url = "https://github.com/klok-opensource/swedish-holidays"
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}